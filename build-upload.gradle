/*
 * MAVEN上传(中心库/本地库)
 *
 * 注意:需要在C:\Users\<user>\.gradle\gradle.properties中配置如下信息
 * signing.keyId=XXXXXXXX(gpg -K 查看私钥ID)
 * signing.password=XXXXXXXX(私钥保存的秘钥)
 * signing.secretKeyRingFile=C:/Users/<user>/AppData/Roaming/gnupg/secring.gpg
 * sonatype_user=sonatype用户名
 * sonatype_password=sonatype密码
 *
 * gradle-local.properties中选择上传到中心库还是本地库
 *
 */

//都加上这个插件, 避免在根节点执行uploadArchives时出错
subprojects {
    apply plugin: 'maven'
}

//给库模块加入上传任务
configure(subprojects.findAll { it.name.startsWith('thistle-') }) {

    apply plugin: 'signing'

    //编译时产生源代码jar
    task generateSourcesJar(type: Jar) {
        from sourceSets.main.java.srcDirs
        classifier 'sources'
    }

    if (upload_target == 'maven') {

        //编译时产生JAVADOC
        task javadocJar(type: Jar, dependsOn: javadoc) {
            from javadoc.destinationDir
            classifier = 'javadoc'
        }

        //禁用JAVA8的JAVADOC检查
        if (JavaVersion.current().isJava8Compatible()) {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }

        //对jar包签名
        signing {
            sign configurations.archives
        }

    }

    //将源码和JAVADOC加入artifacts
    artifacts {
        archives generateSourcesJar
        if (upload_target == 'maven') {
            archives javadocJar
        }
    }

    //上传
    uploadArchives {
        repositories.mavenDeployer {

            if (upload_target == 'maven') {

                //为Pom文件做数字签名
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                //部署到中央库
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatype_user, password: sonatype_password)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatype_user, password: sonatype_password)
                }

            } else if (upload_target == 'private') {

                //部署到Nexus私服
                repository(url: private_release_url) {
                    authentication(userName: private_user, password: private_password)
                }
                snapshotRepository(url: private_snapshot_url) {
                    authentication(userName: private_user, password: private_password)
                }

//				repository(url: "http://localhost:8081/nexus/content/repositories/reponame/") {
//					authentication(userName: 'name', password: 'password')
//				}

            } else {

                //部署到本地仓库
                repository(url: uri(repository_localRepositoryPath))

            }

            pom.groupId = project.group
            pom.artifactId = project.name
            pom.version = project.version

            //构造项目的Pom文件，参见Part 2中Pom文件的规范，不要遗漏必填项
            pom.project {
                name project.name
                packaging 'jar'
                description 'Comprehensive Java common library (Java7+)'
                url 'https://github.com/shepherdviolet/thistle'

                scm {
                    url 'scm:git@github.com:shepherdviolet/thistle.git'
                    connection 'scm:git@github.com:shepherdviolet/thistle.git'
                    developerConnection 'git@github.com:shepherdviolet/thistle.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'shepherdviolet'
                    }
                }

                developers {
                    developer {
                        id 'shepherdviolet'
                        name 'Shepherd Violet'
                        email 'shepherdviolet@163.com'
                    }
                }
            }
        }
    }

}